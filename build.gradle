buildscript {
  dependencies {
    classpath 'org.xtext:xtext-gradle-plugin:1.0.4'
  }
}

plugins {
	id 'net.researchgate.release' version '2.2.0'
	id 'com.jfrog.bintray' version '1.7'
}

allprojects {
	repositories {
		jcenter()
	}
}

subprojects {
	apply plugin: 'org.xtext.xtend'
	apply plugin: 'maven'
	group = 'io.franzbecker.xtend'

	ext.githubRepo = 'franzbecker/xtendutils'
	ext.versions = [
		xtend: '2.10.0',
		junit: '4.12'
	]

	apply from: "$rootDir/gradle/publishing.gradle"

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	dependencies {
		compile "org.eclipse.xtend:org.eclipse.xtend.lib:$versions.xtend"
		compile 'com.google.code.findbugs:jsr305:3.0.0'
		compile 'com.google.inject:guice:4.0'
		testCompile "junit:junit:$versions.junit"
		testCompile 'org.assertj:assertj-core:3.0.0'

		// Provide de.xtendutils.junit as a test-dependency to every other subproject
		if (project.name != 'junit') {
			testCompile project(':junit')
		}
	}

	sourceSets {
  		main.xtendOutputDir = 'src/main/xtend-gen'
  		test.xtendOutputDir = 'src/test/xtend-gen'
	}

}

/*
 * Configure release plugin.
 * Remove tasks "updateVersion" and "commitNewVersion" as we don't want the new SNAPSHOT
 * version commit on the master branch.
 */
tasks.release.tasks -= ['updateVersion', 'commitNewVersion']
release {
	preTagCommitMessage = '[release] - pre tag commit: '
	tagCommitMessage = '[release] - creating tag: '
	newVersionCommitMessage = '[release] - new version commit: '
}
